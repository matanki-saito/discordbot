buildscript {
    ext.versions = [
            // discord client library
            // https://github.com/DV8FromTheWorld/JDA
            'JDA'                  : '5.0.0-beta.22',

            // discord pagination util
            // https://github.com/ygimenez/Pagination-Utils
            'JDAUtils'             : '4.0.6',

            // apache common Lang
            // https://commons.apache.org/
            'apacheCommonLang'     : '3.12.0',

            // log
            // http://www.slf4j.org/
            'slf4jApi'             : '2.0.12',

            // elastic search（テキスト検索）
            // https://www.elastic.co/guide/en/elasticsearch/client/java-api/current/index.html
            'elasticsearch'        : '8.13.1',

            // エラーDiscord通知
            // https://github.com/napstr/logback-discord-appender
            'logbackDiscord'       : '1.0.0',

            // 日付処理用
            // https://qiita.com/tag1216/items/25a64bba2bde98ea88d3
            'jacksonDatatypeJsr310': '2.13.2',

            // test用
            // https://assertj.github.io/doc/
            'assertj'              : '3.22.0',

            // lombok
            // https://mvnrepository.com/artifact/org.projectlombok/lombok
            'lombok'               : '1.18.24',

            // micrometer
            // spring actuatorがprometheus形式でmetrixを出力するために必要
            'micrometer'           : '1.12.4',

            // PDXテキストの展開
            'rico'                 : '0.0.11'
    ]
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version "1.0.11.RELEASE"
    // Dockerhubにdocker imageをuploadするのに必要
    // https://github.com/palantir/gradle-docker
    id 'com.palantir.docker' version "0.36.0"
    // リリース処理に必要
    // https://github.com/researchgate/gradle-release
    id 'net.researchgate.release' version "2.8.1"
    // actuator apiでコミットversionを確認する
    // https://plugins.gradle.org/plugin/com.gorylenko.gradle-git-properties
    id "com.gorylenko.gradle-git-properties" version "2.4.0"
    // githubのリリースを登録するのに必要
    // https://github.com/ajoberstar/gradle-git-publish
    id 'org.ajoberstar.git-publish' version '4.1.0'
}

group = 'com.popush'
sourceCompatibility = 17
targetCompatibility = 17

gitProperties {
    customProperty 'version', version
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url = uri("https://maven.pkg.github.com/matanki-saito/pdx-txt-java")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER_NAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

bootJar {
    archiveBaseName = 'henrietta-docker'
    archiveVersion = '1.0.0'
}

// コメントに日本語があるので必要
javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
}

tasks.register('unpack', Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile as Object))
    into("build/dependency")
}

gitPublish {
    repoUri = "git@github.com:matanki-saito/discordbot.git"
    branch = 'master'

    contents {
        from(javadoc) {
            into 'docs/javadoc'
        }
    }

    preserve {
        include '**'
        exclude 'docs'
    }

    commitMessage = '[skip ci] Publishing a new page'
}

docker {
    name "gnagaoka/henrietta-app:" + version
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}

// junit 5対策
// https://stackoverflow.com/questions/30474767/no-tests-found-for-given-includes-error-when-running-parameterized-unit-test-in
test {
    useJUnitPlatform()
}

release {
    // [skip ci]をコメントに入れないと、無限ループする
    preTagCommitMessage = '[skip ci] [Gradle Release Plugin] - pre tag commit: '
    tagCommitMessage = '[skip ci] [Gradle Release Plugin] - creating tag: '
    newVersionCommitMessage = '[skip ci] [Gradle Release Plugin] - new version commit: '
}

dependencies {
    // spring boot
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-cache")
    implementation("org.springframework.boot:spring-boot-starter-data-redis")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    compileOnly('org.springframework.boot:spring-boot-configuration-processor')
    testImplementation('org.springframework.boot:spring-boot-starter-test')

    // log, monitoring
    implementation("org.slf4j:slf4j-api:${versions.slf4jApi}")
    implementation("io.micrometer:micrometer-core")
    implementation("io.micrometer:micrometer-registry-prometheus")
    implementation("io.micrometer:micrometer-observation")
    implementation("com.github.napstr:logback-discord-appender:${versions.logbackDiscord}")

    // 外部SDK
    implementation("net.dv8tion:JDA:${versions.JDA}")
    implementation("com.github.ygimenez:Pagination-Utils:${versions.JDAUtils}")
    implementation("co.elastic.clients:elasticsearch-java:${versions.elasticsearch}")
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.17.4'
    //implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation group: 'org.kohsuke', name: 'github-api', version: '1.313'

    // service
    implementation("com.github.matanki_saito:rico:${versions.rico}")

    // utils
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${versions.jacksonDatatypeJsr310}")
    implementation("org.apache.commons:commons-lang3:${versions.apacheCommonLang}")
    compileOnly("org.projectlombok:lombok:${versions.lombok}")
    annotationProcessor("org.projectlombok:lombok:${versions.lombok}")
    testImplementation("org.projectlombok:lombok:${versions.lombok}")

    // test
    testImplementation("org.assertj:assertj-core:${versions.assertj}")

}
